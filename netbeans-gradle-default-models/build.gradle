apply plugin: 'java';

group = 'com.github.kelemen';
version = '1.2.8.2-SNAPSHOT';
sourceCompatibility = '1.5';

ext.scriptFile = { String fileName ->
    return new File(new File(rootDir, 'common-scripts'), fileName);
}

def compilerOptions = [compileJava, compileTestJava]*.options;
compilerOptions*.encoding = 'UTF-8';
compilerOptions*.compilerArgs = ['-Xlint'];

task sourcesJar(type: Jar, dependsOn: classes, description: 'Creates a jar from the source files.') {
    classifier = 'sources';
    from sourceSets.main.allSource;
}

artifacts {
    archives jar
    archives sourcesJar
}

repositories {
    mavenCentral();

    maven {
        url 'http://repo.gradle.org/gradle/libs-releases-local'
    }

    maven {
        url 'http://repo.gradle.org/gradle/libs-snapshots-local'
    }
}

configurations {
    provided
}

dependencies {
    compile group: 'org.gradle', name: 'gradle-tooling-api', version: '1.9';
    provided group: 'org.codehaus.groovy', name: 'groovy-all', version: '1.8.6';

    testCompile group: 'junit', name: 'junit', version: '4.11';
    testCompile group: 'org.mockito', name: 'mockito-core', version: '1.9.5';
}

sourceSets.main.compileClasspath += configurations.provided

List gradleVersions = ['1.8', '1.9'];
Set daemonTestPackagePatterns = ['org/netbeans/gradle/model/java/**'];

task testAll {
    dependsOn test;
}

gradleVersions.each { String gradleVersion ->
    def currentTask = task(['type': Test], "testGradle${gradleVersion}", { testTask ->
        testTask.systemProperty 'TESTED_GRADLE_DAEMON_VERSION', gradleVersion;
        daemonTestPackagePatterns.each { pattern ->
            testTask.include pattern;
        }
    });
    testAll.dependsOn currentTask;
}

if (project.hasProperty('excludeDuplicatesFromTestTask')) {
    test.systemProperty 'SKIP_GRADLE_DAEMON_TESTS', project.excludeDuplicatesFromTestTask;
}

afterEvaluate {
    System.setProperty('line.separator', '\n');
}

apply from: scriptFile('maven-publish.gradle');
