apply plugin: 'groovy';

group = 'com.github.kelemen';
version = '1.2.6.1-SNAPSHOT';
sourceCompatibility = '1.5';

ext.scriptFile = { String fileName ->
    return new File(new File(rootDir, 'common-scripts'), fileName);
}

def compilerOptions = [compileJava, compileTestJava]*.options;
compilerOptions*.encoding = 'UTF-8';
compilerOptions*.compilerArgs = ['-Xlint'];

task sourcesJar(type: Jar, dependsOn: classes, description: 'Creates a jar from the source files.') {
    classifier = 'sources';
    from sourceSets.main.allSource;
}

artifacts {
    archives jar
    archives sourcesJar
}

sourceSets {
    secondary {
        compileClasspath += main.output;
        compileClasspath += configurations.compile;

        runtimeClasspath += main.output;
        runtimeClasspath += configurations.runtime;
    }
}

jar.from sourceSets.secondary.output;

// Test (when run) must depend on the JAR file for now to workaround a classpath
// issue in the new Tooling API.
test.dependsOn jar
sourceSets.test.runtimeClasspath -= sourceSets.main.output;
sourceSets.test.runtimeClasspath += files(jar.archivePath);

sourceSets.test.compileClasspath += sourceSets.secondary.output;

apply plugin: 'idea';

idea {
    module {
        sourceDirs += sourceSets.secondary.allSource.srcDirs;
    }
}

repositories {
    mavenCentral();

    maven {
        url 'http://repo.gradle.org/gradle/libs-releases-local'
    }

    maven {
        url 'http://repo.gradle.org/gradle/libs-snapshots-local'
    }
}

dependencies {
    compile group: 'org.gradle', name: 'gradle-tooling-api', version: '1.8-rc-1';
    compile group: 'org.codehaus.groovy', name: 'groovy-all', version: '1.8.6';

    testCompile group: 'junit', name: 'junit', version: '4.11';
    testCompile group: 'org.mockito', name: 'mockito-core', version: '1.9.5';
}

afterEvaluate {
    System.setProperty('line.separator', '\n');
}

apply from: scriptFile('maven-publish.gradle');
